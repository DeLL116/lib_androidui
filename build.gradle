apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "store", "api", "device"

    productFlavors {
        // --> Store Flavor Dimension Types for different app store platforms
        //     Google = Google Play // Amazon = Amazon App Store
        google {
            dimension "store"
        }
        amazon {
            dimension "store"
        }

        // --> API Flavor Dimension Types for different build platforms
        //     Prod = Production // Stage = Staging // dev = development
        prod {
            dimension "api"
        }
        stage {
            dimension "api"
        }
        dev {
            dimension "api"
        }

        // --> API Flavor Dimension Types for different device form-factor profiles
        //     Phone = Mobile Phone // Tablet = a Tablet // TV = 10ft Leanback
        phone {
            dimension "device"
        }
        tablet {
            dimension "device"
        }
        tv {
            dimension "device"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // --> Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    api 'androidx.test.espresso:espresso-idling-resource:3.1.1'                         // Note...uses api not testApi!

    // **** The following are not duplicates, but rather a necessary evil for now ****
    // These dependencies are needed for the classes contain in
    // the *debug* source-sets folder for com.nochino.support.androidui.testing
    // These dependencies must be referenced as a normal
    // implementations,  but FOR DEBUG BUILDS ONLY. Using these in release
    // builds that are obfuscated with ProGuard will cause build failures!
    // TODO :: Create module solely for instrumentation tests -->
    // --> See https://developer.android.com/studio/test/#use_separate_test_modules_for_instrumented_tests
    debugImplementation 'androidx.test:rules:1.1.1'
    debugImplementation 'androidx.test:runner:1.1.1'
    debugImplementation 'androidx.test:core-ktx:1.1.0'
    debugImplementation 'androidx.test.ext:junit:1.1.0'
    debugImplementation 'androidx.test.ext:junit-ktx:1.1.0'
    debugImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    debugImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'

    // --> Android Support / UI
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha01'              // ViewModel / LiveData
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'

    // --> Navigation
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha09'
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-alpha09'

    // --> Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // --> Square / Jake Wharton
    implementation 'com.squareup.picasso:picasso:2.71828'                               // Image Loading
    implementation 'com.jakewharton.timber:timber:4.7.1'                                // Enhanced Logging
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'              // Detecting Memory Leaks
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'      // Detecting Memory Leaks
}
